#!/usr/bin/env perl6
use v6.c;

use DBIish;

sub targetfile($filename, $extension, --> Str) {
    my $basename = $filename.IO.extension: '';
    my $newname = $basename.IO.extension($extension, :0parts).absolute;
    # Don't clobber existing files
    while $newname.IO.e {
        $newname = $basename.IO.extension(("a".."z").pick(5).join, :0parts).extension($extension, :0parts).absolute;
    }
    $newname;
}

sub create-database($databasefile where all(*.ends-with('sql'), *.IO.f, *.IO.e)) {
    note "creating an sqlite3 database from your sql file";
    my $newname = targetfile($databasefile, "sqlite3");
    note "sqlite3 file path is $newname";
    my $proc = run 'sqlite3', '-init', $databasefile, $newname, :in;
    # Don't want to send any commands to the sqlite database.
    $proc.in.close;
    $newname;
}

multi sub MAIN($databasefile is copy where all(*.ends-with('sqlite3' | 'sql'), *.IO.f, *.IO.e), Str :$imagetype = "svg") {
    if $databasefile.ends-with('sql') {
        $databasefile = create-database($databasefile);
    }

    my $dbh = DBIish.connect("SQLite", :database($databasefile));

    my $imagefile = targetfile($databasefile, $imagetype);
    note "image file path is $imagefile";

    my $dotproc = run 'dot', "-T$imagetype", "-o", $imagefile, :in;

    my $query = $dbh.prepare(q:to/STMT/);
        select 
            routines.name as name,
            routines.file as file,
            routines.line as line,

            calls.rec_depth as depth,
            calls.id        as id,
            calls.parent_id as parent_id,

            calls.exclusive_time   as exclusive,
            parent.inclusive_time  as parent_inclusive,
            calls.inclusive_time   as inclusive,
            calls.entries          as entries

            from calls inner join routines on calls.routine_id = routines.id
                       inner join calls as parent on calls.parent_id = parent.id

            order by inclusive desc
                       ;
        STMT

    my %file_colors;

    $dotproc.in.say: 'digraph G {';
    $dotproc.in.say: '    graph [rankdir="LR", splines="line"];';
    $dotproc.in.say: '    node  [shape="box"];';

    sub percentage_fill($part, $whole) {
        my $percentage = ($part / ($whole || Inf)).round(0.05);
        if $part == $whole { $percentage = 1.0 }
        my $fill;
        $fill = "blue;{$percentage.round(0.05)}:white";
        $fill = "white" if $percentage == 0;
        $fill = "blue" if $percentage == 1;
        $fill;
    }

    $query.execute();
    for $query.allrows(:array-of-hash) {
        unless $_<name> {
            if $_<file>.chars > 15 {
                $_<name> = $_<file>.substr(*-15)
            } else {
                $_<name> = $_<file>;
            }
            $_<name> ~= "\@$_<line>";
        }
        $_<name> .= trans(["&", "<", ">"] => ["&amp;", "&lt;", "&gt;"]);

        my $color = (%file_colors{$_<file>} //= "{rand.substr(0,5)} {(rand * 0.4).substr(0,5)} {(rand * 0.4 + 0.6).substr(0,5)}");

        my $edgefill = percentage_fill($_<inclusive>, $_<parent_inclusive>);
        my $nodefill = percentage_fill($_<exclusive>, $_<inclusive>);

        $dotproc.in.say: qq[    "$_<id>" [label=<<table border="0">
                                <tr><td>{$_<name>}</td>
                                <td><table border="0" cellborder="1"><tr><td bgcolor="$nodefill" width="20" height="8"></td></tr></table>
                            </td></tr></table>>,
                             style="filled", shape="box", color="$color", tooltip="$_<file>"];];
        $dotproc.in.say: qq[        "$_<parent_id>" -> "$_<id>"
                          [label=<<table border="0"><tr>
                              <td>{$_<entries>}x</td>
                              <td><table border="0" cellborder="1"><tr><td bgcolor="$edgefill" width="20" height="4"></td></tr></table></td>
                              </tr></table>>,
                           shape="box"
                          ];];
    }

    $dotproc.in.say: '}';

    $dotproc.in.close;

    $query.finish();

    note "don't forget to scroll down in the picture!";
}
